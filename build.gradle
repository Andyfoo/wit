plugins {
  id "io.spring.dependency-management" version "1.0.7.RELEASE"
}

group 'org.febit.wit'
version '2.8.0-SNAPSHOT'

ext {
  versions = [
      slf4j         : '1.7.25',
      commonsLogging: '1.1',
      servletApi    : '2.5',
      junitJupiter  : '5.5.0',
      mockito       : '3.0.0'
  ]
}

subprojects {

  group rootProject.group
  version rootProject.version

  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  apply plugin: 'io.spring.dependency-management'

  apply from: "${rootDir}/gradle/publish-maven.gradle"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenCentral()
  }

  tasks.withType(Test) {
    testLogging.events = ["FAILED", "PASSED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"]
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.debug = true
    options.compilerArgs << "-Xlint:unchecked"
    options.compilerArgs << "-Xlint:deprecation"
  }

  tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
  }

  test {
    useJUnitPlatform()
  }
  
  jar {
    manifest {
      attributes(
          'Implementation-Title': project.name,
          'Implementation-Vendor': 'febit.org',
          'Implementation-Vendor-Id': project.group,
          'Implementation-Version': version.toString(),
          'Created-By': "Gradle ${project.getGradle().getGradleVersion()}",
          'Build-Jdk': System.getProperty('java.version'),
          'Build-Time': "${Instant.now()}"
      )
    }
  }

  dependencyManagement {
    dependencies {
      dependencySet(group: 'org.slf4j', version: "${versions.slf4j}") {
        entry 'slf4j-api'
        entry 'slf4j-simple'
      }
      dependencySet(group: 'org.junit.jupiter', version: "${versions.junitJupiter}") {
        entry 'junit-jupiter-api'
        entry 'junit-jupiter-engine'
      }
      dependency "commons-logging:commons-logging-api:${versions.commonsLogging}"
      dependency "javax.servlet:servlet-api:${versions.servletApi}"
      dependency "junit:junit:${versions.junit}"
      dependency "org.mockito:mockito-junit-jupiter:${versions.mockito}"
    }
  }

  dependencies {
    testCompile 'org.slf4j:slf4j-api'
    testCompile 'org.junit.jupiter:junit-jupiter-api'
    testCompile 'org.mockito:mockito-junit-jupiter'

    testRuntime 'org.junit.jupiter:junit-jupiter-engine'
    testRuntime 'org.slf4j:slf4j-simple'
  }
}

wrapper {
  gradleVersion = '5.4.1'
}
